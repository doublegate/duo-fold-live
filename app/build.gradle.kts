plugins { id("com.android.application"); id("org.jetbrains.kotlin.android"); id("org.jetbrains.kotlin.plugin.compose") }
android {
 namespace = "org.duofold.live"
 compileSdk = 36
 defaultConfig { applicationId = "org.duofold.live"; minSdk = 34; targetSdk = 36; versionCode = 1082; versionName = "3.5.2-a16.11" }
 val releaseKey = System.getenv("DUO_KEYSTORE")
 signingConfigs { if (releaseKey != null) create("standalone") {
  storeFile=file(releaseKey); storePassword=System.getenv("DUO_STORE_PASSWORD")
  keyAlias=System.getenv("DUO_KEY_ALIAS"); keyPassword=System.getenv("DUO_KEY_PASSWORD")
 } }
 buildTypes {
  getByName("release") { isMinifyEnabled=false; isShrinkResources=false; proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt")); if (releaseKey != null) signingConfig=signingConfigs.getByName("standalone"); buildConfigField("boolean","DIAGNOSTICS","false") }
  getByName("debug") { buildConfigField("boolean","DIAGNOSTICS","true") }
  // Release-optimized, non-debuggable build with the change-only diagnostic logs, signed with the local debug
  // key so it updates an installed debug build in place (settings, accessibility and Shizuku grants survive).
  // Debug builds run Compose noticeably slower; judge animation smoothness on this variant.
  create("fold7test") { initWith(getByName("release")); signingConfig=signingConfigs.getByName("debug"); isDebuggable=false; matchingFallbacks += listOf("release"); buildConfigField("boolean","DIAGNOSTICS","true") }
 }
 buildFeatures { compose=true; buildConfig=true }
 compileOptions { sourceCompatibility=JavaVersion.VERSION_17; targetCompatibility=JavaVersion.VERSION_17 }
 kotlinOptions { jvmTarget="17" }
 // Lint gate (CI runs lintRelease): existing findings live in lint-baseline.xml, so any NEW warning fails the
 // build. Disabled: hidden-API reflection is this app's design (PrivateApi), and the version checks change
 // verdicts whenever a dependency publishes a release, failing unchanged code (keep dependency bumps deliberate).
 lint {
  baseline = file("lint-baseline.xml")
  warningsAsErrors = true
  abortOnError = true
  disable += setOf("PrivateApi", "DiscouragedPrivateApi", "GradleDependency", "AndroidGradlePluginVersion", "NewerVersionAvailable")
 }
}
val wallpaperStubs by tasks.registering(JavaCompile::class) {
 source(rootProject.fileTree("wallpaper-stubs") { include("**/*.java") })
 classpath = files(android.bootClasspath)
 destinationDirectory.set(layout.buildDirectory.dir("wallpaper-stubs"))
 sourceCompatibility = "17"
 targetCompatibility = "17"
}
val wallpaperStubJar by tasks.registering(Jar::class) {
 dependsOn(wallpaperStubs)
 from(wallpaperStubs.map { it.destinationDirectory })
 archiveFileName.set("wallpaper-framework-stubs.jar")
 destinationDirectory.set(layout.buildDirectory.dir("compile-only"))
}
dependencies {
 compileOnly(files(wallpaperStubJar))
 implementation("org.lsposed.hiddenapibypass:hiddenapibypass:6.1")
 implementation(fileTree("libs") { include("*.jar") })
 implementation("androidx.window:window:1.5.1")
 implementation("androidx.profileinstaller:profileinstaller:1.4.1")
 implementation(platform("androidx.compose:compose-bom:2025.06.01"))
 implementation("androidx.activity:activity-compose:1.10.1")
 implementation("androidx.lifecycle:lifecycle-runtime-compose:2.9.1")
 implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.9.1")
 implementation("androidx.compose.ui:ui")
 implementation("androidx.compose.foundation:foundation")
 implementation("androidx.compose.material3:material3")
 testImplementation("junit:junit:4.13.2")
}
